buildscript {
  ext.DEFAULT_KOTLIN_VERSION = '1.5.10'
  ext.safeExtGet={prop, fallback-> 
    return rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }
    repositories {
    google()
    mavenCentral()
  }


  dependencies {
    classpath 'com.android.tools.build:gradle:4.2.2'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${safeExtGet('kotlinVersion',DEFAULT_KOTLIN_VERSION)}"
  }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'


def DEFAULT_COMPILE_SDK_VERSION = 30
def DEFAULT_BUILD_TOOLS_VERSION = "30.0.2"
def DEFAULT_MIN_SDK_VERSION = 16
def DEFAULT_TARGET_SDK_VERSION = 30
def DEFAULT_PLAY_SERVICES_VERSION = "17.6.0"

android {
  compileSdkVersion safeExtGet("compileSdkVersion", DEFAULT_COMPILE_SDK_VERSION)

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
    targetSdkVersion safeExtGet('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
    versionCode 3
    versionName "3.0.0"
  }
  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  flavorDimensions "store"
  productFlavors {
    amazon{
      dimension "store"
    }
    play{
      dimension "store"
    }
  }
  testOptions {
    unitTests.returnDefaultValues = true
  }
}

repositories {
  maven {
            // All of React Native (JS, Android binaries) is installed from npm
            url "$rootDir/../node_modules/react-native/android"
        }
  google()
  mavenCentral()
}

dependencies {
  implementation 'com.facebook.react:react-native:+'
  testImplementation 'junit:junit:4.12'
  testImplementation "io.mockk:mockk:1.12.4"
  playImplementation 'com.android.billingclient:billing:4.0.0'
  def playServicesVersion = safeExtGet('playServicesVersion', DEFAULT_PLAY_SERVICES_VERSION)
  playImplementation "com.google.android.gms:play-services-base:$playServicesVersion"
  amazonImplementation fileTree(dir: 'libs', include: ['*.jar'])

  def supportLibVersion = safeExtGet('supportLibVersion', safeExtGet('supportVersion', null))
  def androidXVersion = safeExtGet('androidXVersion', null)
  if (supportLibVersion && androidXVersion == null) {
    implementation "com.android.support:support-annotations:$supportLibVersion"
    implementation "com.android.support:customtabs:$supportLibVersion"
  } else {
    def defaultAndroidXVersion = "1.2.0"
    if (androidXVersion == null) {
      androidXVersion = defaultAndroidXVersion
    }
    def androidXAnnotation = safeExtGet('androidXAnnotation', androidXVersion)
    def androidXBrowser = safeExtGet('androidXBrowser', androidXVersion)
    implementation "androidx.annotation:annotation:$androidXAnnotation"
    implementation "androidx.browser:browser:$androidXBrowser"
  }
}
